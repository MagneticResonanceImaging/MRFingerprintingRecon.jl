{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Non-Cartesian MRI"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "This example illustrates how to perform an MRI subspace reconstruction from non-Cartesian k-space data.\n",
    "For this example, we need the following packages:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using MRFingerprintingRecon\n",
    "using ImagePhantoms\n",
    "using LinearAlgebra\n",
    "using IterativeSolvers\n",
    "using FFTW\n",
    "using NonuniformFFTs\n",
    "using Plots\n",
    "using Random"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We first simulate some data from a Shepp-Logan phantom and generate some coil maps using\n",
    "various phase modulations:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "T = Float32\n",
    "Nx = 32\n",
    "Nc = 4 # nr of coefficients in the temporal subspace\n",
    "Nt = 20 # nr of acquired time frames per cycle\n",
    "Ncyc = 10 # nr of cycles (i.e., repeats of flip angle pattern)\n",
    "img_shape = (Nx, Nx) # 2D image in this example\n",
    "\n",
    "# create test image\n",
    "x = zeros(Complex{T}, Nx, Nx, Nc)\n",
    "x[:, :, 1] = transpose(shepp_logan(Nx))\n",
    "x[1:end÷2, :, 1] .*= exp(1im * π / 3)\n",
    "x[:, :, 2] = shepp_logan(Nx)\n",
    "\n",
    "# coil maps\n",
    "Ncoil = 9\n",
    "cmaps = ones(Complex{T}, Nx, Nx, Ncoil)\n",
    "[cmaps[i, :, 2] .*= exp(1im * π * i / Nx) for i ∈ axes(cmaps, 1)]\n",
    "[cmaps[i, :, 3] .*= exp(-1im * π * i / Nx) for i ∈ axes(cmaps, 1)]\n",
    "[cmaps[:, i, 4] .*= exp(1im * π * i / Nx) for i ∈ axes(cmaps, 2)]\n",
    "[cmaps[:, i, 5] .*= exp(-1im * π * i / Nx) for i ∈ axes(cmaps, 2)]\n",
    "[cmaps[i, :, 6] .*= exp(2im * π * i / Nx) for i ∈ axes(cmaps, 1)]\n",
    "[cmaps[i, :, 7] .*= exp(-2im * π * i / Nx) for i ∈ axes(cmaps, 1)]\n",
    "[cmaps[:, i, 8] .*= exp(2im * π * i / Nx) for i ∈ axes(cmaps, 2)]\n",
    "[cmaps[:, i, 9] .*= exp(-2im * π * i / Nx) for i ∈ axes(cmaps, 2)]\n",
    "\n",
    "# coil maps are defined as vectors of complex n-dimensional arrays\n",
    "for i ∈ CartesianIndices(@view cmaps[:, :, 1])\n",
    "    cmaps[i, :] ./= norm(cmaps[i, :])\n",
    "end\n",
    "cmaps = [cmaps[:, :, ic] for ic = 1:Ncoil]"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Next, we set up a kooshball trajectory for data acquisition and generate a set of basis functions. The non-Cartesian methods\n",
    "use float trajectories in range $k \\in [-0.5, 0.5)$, as opposed to integer trajectories for Cartesian methods."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# set up trajectory\n",
    "α_g = 2π / (1 + √5)\n",
    "phi = Float32.(α_g * (1:Nt*Ncyc))\n",
    "theta = Float32.(0 * (1:Nt*Ncyc) .+ pi / 2)\n",
    "phi = reshape(phi, Ncyc, Nt)\n",
    "theta = reshape(theta, Ncyc, Nt)\n",
    "\n",
    "# generate trj as (2, Nx*Ncyc, Nt)-shaped array\n",
    "# alternatively, trj can be shaped as (ndims, n_ADC, n_readouts, n_timeframes)\n",
    "trj = traj_kooshball(2Nx, theta, phi) # shaped (ndims, nsamples, n_timeframes)\n",
    "trj = trj[1:2, :, :] # use only the first 2 dims as a 2D k-space trj\n",
    "\n",
    "# set up basis functions\n",
    "U = randn(Complex{T}, Nt, Nc)\n",
    "U, _, _ = svd(U)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We use the phantom image `x`, the coil maps `cmaps`, the trajectory `trj`, and the basis functions `U` to simulate some k-space data:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# simulate data as (2Nx*Ncyc, Nt, Ncoil)-shaped array\n",
    "# alternatively, data can be shaped as (2Nx, Ncyc, Nt, Ncoil)\n",
    "data = Array{Complex{T},3}(undef, 2Nx * Ncyc, Nt, Ncoil)\n",
    "nfftplan = PlanNUFFT(Complex{T}, img_shape; fftshift=true)\n",
    "xcoil = copy(x)\n",
    "\n",
    "for icoil ∈ axes(data, 3)\n",
    "    xcoil .= x\n",
    "    xcoil .*= cmaps[icoil]\n",
    "    for it ∈ axes(data, 2)\n",
    "        set_points!(nfftplan, NonuniformFFTs._transform_point_convention.(reshape(trj[:, :, it], 2, :)))\n",
    "        xt = reshape(reshape(xcoil, :, Nc) * U[it, :], Nx, Nx)\n",
    "        # simulate data from image using type-2 (uniform to non-uniform) NUFFT\n",
    "        @views NonuniformFFTs.exec_type2!(data[:, it, icoil], nfftplan, xt)\n",
    "    end\n",
    "end"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "The data format uses either 3D or 4D arrays, where the 4D format is used to place ADC points within a separate array axis from the total number of samples.\n",
    "Internally, all code relies on 3D arrays and the 4D arrays are handled by wrappers.\n",
    "Furthermore, reconstructions can make use of a binary mask to exclude specific samples from being included in the reconstruction.\n",
    "To illustrate the data removal, we create a mask that removes one time frame from one cycle:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# create sampling mask\n",
    "it_rm = 1\n",
    "icyc_rm = 5\n",
    "sample_mask = trues(2Nx, Ncyc, Nt)\n",
    "sample_mask[:, icyc_rm, it_rm] .= false\n",
    "sample_mask = reshape(sample_mask, 2Nx*Ncyc, Nt) # masks are 2- or 3-dim depending on format of trj and data"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Coil maps may also be auto-calibrated from k-space measurements using ESPIRiT:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "cmaps = calculate_coil_maps(data, trj, img_shape; U)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Now, we can compute the normal operator and the adjoint NUFFT (backprojection) with the specified sampling mask:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "AᴴA = NFFTNormalOp(img_shape, trj, U; cmaps, sample_mask)\n",
    "b = calculate_backprojection(data, trj, cmaps; U, sample_mask)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "GPU-methods are called through multiple dispatch, i.e., when objects of type `CuArray` are passed as arguments. The normal operator `A` and the backprojection `b` are compatible with the iterative solvers from [IterativeSolvers.jl](https://github.com/JuliaLinearAlgebra/IterativeSolvers.jl) and\n",
    "[RegularizedLeastSquares.jl](https://github.com/JuliaImageRecon/RegularizedLeastSquares.jl). This enables solving the inverse problem with various algorithms, including conjugate gradient (CG):"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# solve inverse problem with CG\n",
    "xr = cg(AᴴA, vec(b), maxiter=20)\n",
    "xr = reshape(xr, Nx, Nx, Nc) # reshape vector back to 2D image with Nc coefficients"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
