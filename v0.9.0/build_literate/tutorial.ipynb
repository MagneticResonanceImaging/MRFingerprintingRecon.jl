{
 "cells": [
  {
   "cell_type": "markdown",
   "source": [
    "# Non-Cartesian MRI"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "This example illustrates how to perform an iterative multi-coil MRI subspace reconstruction from non-Cartesian k-space data\n",
    "with the conjugate gradient algorithm. To perform such a reconstruction, we need the following packages:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using MRISubspaceRecon\n",
    "using IterativeSolvers # for conjugate gradient reconstruction\n",
    "using Plots"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# Data simulation"
   ],
   "metadata": {}
  },
  {
   "cell_type": "markdown",
   "source": [
    "We first simulate some data from a Shepp-Logan phantom and generate some coil maps using\n",
    "various phase modulations. The below packages are only used for the data simulation:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "using FFTW\n",
    "using ImagePhantoms\n",
    "using NonuniformFFTs\n",
    "using Random\n",
    "using LinearAlgebra\n",
    "\n",
    "Nx = 32\n",
    "Nc = 2 # nr of coefficients in the temporal subspace\n",
    "Nt = 10 # nr of acquired time frames per cycle\n",
    "Ncyc = 20 # nr of cycles (i.e., repeats of flip angle pattern)\n",
    "Ncoil = 2\n",
    "img_shape = (Nx, Nx) # 2D image in this example\n",
    "\n",
    "# create test coefficient image\n",
    "x = zeros(ComplexF32, Nx, Nx, Nc)\n",
    "x[:, :, 1] = transpose(shepp_logan(Nx)) .* exp(1im * π / 3)\n",
    "x[:, :, 2] = shepp_logan(Nx)\n",
    "\n",
    "p = heatmap(abs.(x[:,:,1]), layout=(1,2), subplot=1, ticks=[], colorbar=false, size=(700,350), title=\"coeff. 1\")\n",
    "heatmap!(p, abs.(x[:,:,2]), subplot=2, ticks=[], colorbar=false, title=\"coeff. 2\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Next, we set up a set of coil maps and a trajectory for data acquisition. We then generate a set of basis functions. The non-Cartesian methods\n",
    "use float trajectories in range $k \\in [-0.5, 0.5)$, as opposed to integer trajectories for Cartesian methods."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# coil maps as vector of complex arrays\n",
    "cmaps = [ones(ComplexF32, Nx, Nx); ones(ComplexF32, Nx, Nx) .* ComplexF32.(exp(1im * π / 2))]\n",
    "println(\"typeof(cmaps) = $(typeof(cmaps))\")\n",
    "println(\"size(cmaps) = $(size(cmaps))\")\n",
    "\n",
    "# set up a 2D radial trajectory\n",
    "trj = traj_2d_radial_goldenratio(2Nx, Ncyc, Nt) # 2Nx for oversampling\n",
    "\n",
    "# set up basis functions\n",
    "U = randn(ComplexF32, Nt, Nc)\n",
    "U, _, _ = svd(U)\n",
    "println(\"typeof(trj) = $(typeof(trj))\")\n",
    "println(\"typeof(U) = $(typeof(U))\")\n",
    "println(\"size(U) = $(size(U))\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "Finally, we use the phantom image `x`, the coil maps `cmaps`, the trajectory `trj`, and the basis functions `U` to simulate k-space data:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# simulate data as (2Nx*Ncyc, Nt, Ncoil)-shaped array\n",
    "data = Array{ComplexF32,3}(undef, 2Nx * Ncyc, Nt, Ncoil)\n",
    "nfftplan = PlanNUFFT(ComplexF32, img_shape; fftshift=true)\n",
    "for icoil ∈ axes(data, 3)\n",
    "    xcoil = copy(x)\n",
    "    xcoil .*= cmaps[icoil] # scale image by coil map\n",
    "    for it ∈ axes(data, 2)\n",
    "        set_points!(nfftplan, NonuniformFFTs._transform_point_convention.(reshape(trj[:, :, it], 2, :))) # prep NUFFT\n",
    "        xt = reshape(reshape(xcoil, :, Nc) * U[it, :], Nx, Nx)\n",
    "        # simulate data from image using type-2 (uniform to non-uniform) NUFFT\n",
    "        @views NonuniformFFTs.exec_type2!(data[:, it, icoil], nfftplan, xt)\n",
    "    end\n",
    "end\n",
    "println(\"size(data)   = $(size(data))\") # array shape of data\n",
    "println(\"typeof(data) = $(typeof(data))\") # type of input data"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "By default, the data format uses either 3D arrays. Alternatively, 4D arrays can be used as inputs, where the 4D format is used to place ADC points within a separate array axis from the total number of samples.\n",
    "Internally, all code relies on 3D arrays and the 4D arrays are handled by wrappers."
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "data = reshape(data, 2Nx, Ncyc, Nt, Ncoil)\n",
    "trj = reshape(trj, 2, 2Nx, Ncyc, Nt)\n",
    "println(\"size(data) = $(size(data))\")\n",
    "println(\"size(trj)  = $(size(trj))\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# Sensitivity profiles\n",
    "Coil maps may be auto-calibrated from k-space measurements using ESPIRiT:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "cmaps = calculate_coil_maps(data, trj, img_shape; U)\n",
    "println(\"size(cmaps) = $(size(cmaps))\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# Sample mask\n",
    "Furthermore, reconstructions can make use of a binary mask to exclude specific samples from being included in the reconstruction.\n",
    "To illustrate the data removal, we create a mask that removes one time frame from one cycle:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# create sampling mask\n",
    "it_rm = 1\n",
    "icyc_rm = 5\n",
    "sample_mask = trues(2Nx, Ncyc, Nt)\n",
    "sample_mask[:, icyc_rm, it_rm] .= false\n",
    "println(\"typeof(sample_mask) = $(typeof(sample_mask))\")\n",
    "println(\"size(sample_mask) = $(size(sample_mask))\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# Normal operator and adjoint\n",
    "Now, we can compute the normal operator using the sample mask:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "AᴴA = NFFTNormalOp(img_shape, trj, U; cmaps, sample_mask)\n",
    "println(AᴴA)"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "We can also compute the adjoint NUFFT (backprojection) with the specified sampling mask:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "b = calculate_backprojection(data, trj, cmaps; U, sample_mask)\n",
    "println(\"size(b) = $(size(b))\")\n",
    "\n",
    "p = heatmap(abs.(b[:,:,1]), layout=(1,2), subplot=1, ticks=[], colorbar=false, title=\"coeff. 1\", size=(700,350))\n",
    "heatmap!(p, abs.(b[:,:,2]), subplot=2, ticks=[], colorbar=false, title=\"coeff. 2\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "# Iterative solvers\n",
    "The normal operator `A` and the backprojection `b` are compatible with the iterative solvers from [IterativeSolvers.jl](https://github.com/JuliaLinearAlgebra/IterativeSolvers.jl) and\n",
    "[RegularizedLeastSquares.jl](https://github.com/JuliaImageRecon/RegularizedLeastSquares.jl). This enables solving the inverse problem with various algorithms, including conjugate gradient (CG). In this way we can\n",
    "recover the original image from the backprojection:"
   ],
   "metadata": {}
  },
  {
   "outputs": [],
   "cell_type": "code",
   "source": [
    "# solve inverse problem with CG. GPU-methods are called through multiple dispatch, i.e., when objects of type `CuArray` are passed as arguments.\n",
    "xr = cg(AᴴA, vec(b), maxiter=20)\n",
    "xr = reshape(xr, Nx, Nx, Nc) # reshape vector back to 2D image with Nc coefficients\n",
    "\n",
    "p = heatmap(abs.(xr[:,:,1]), layout=(1,2), subplot=1, ticks=[], colorbar=false, size=(700,350), title=\"coeff. 1\")\n",
    "heatmap!(p, abs.(xr[:,:,2]), subplot=2, ticks=[], colorbar=false, title=\"coeff. 2\")"
   ],
   "metadata": {},
   "execution_count": null
  },
  {
   "cell_type": "markdown",
   "source": [
    "---\n",
    "\n",
    "*This notebook was generated using [Literate.jl](https://github.com/fredrikekre/Literate.jl).*"
   ],
   "metadata": {}
  }
 ],
 "nbformat_minor": 3,
 "metadata": {
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.4"
  },
  "kernelspec": {
   "name": "julia-1.12",
   "display_name": "Julia 1.12.4",
   "language": "julia"
  }
 },
 "nbformat": 4
}
